!classDefinition: #AccountSummaryTest category: 'Portfolio-Solucion'!
TestCase subclass: #AccountSummaryTest
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!AccountSummaryTest methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:42:19'!
test01AccountSummaryWithBalanceZero

	| account1  message |
	
	account1 := ReceptiveAccount new.
			
	message := OrderedCollection new.	
	message add: 'Balance = 0'.

	self assert: message equals: (AccountSummary of: account1) value.
! !

!AccountSummaryTest methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:42:30'!
test02AccountSummaryWithDeposit

	| account1 message |
	
	account1 := ReceptiveAccount new.
	Deposit register: 100 on: account1.
	
	
	message := OrderedCollection new.
	message add: 'Deposito por 100'.
	message add: 'Balance = 100'.

	
	self assert: message equals: (AccountSummary of: account1) value.
! !

!AccountSummaryTest methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:42:45'!
test03AccountSummaryWithWithdraw

	| account1 message |
	
	account1 := ReceptiveAccount new.
	Withdraw register: 100 on: account1.
	
	
	message := OrderedCollection new.
	message add: 'Extraccion por 100'.
	message add: 'Balance = -100'.

	
	self assert: message equals:  (AccountSummary of: account1) value.
! !

!AccountSummaryTest methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:42:49'!
test04AccountSummaryWithRecevingTransference

	| account1 account2 message |
	
	account1 := ReceptiveAccount new.
	account2 := ReceptiveAccount new.

	Transference register: 100 from: account2  to: account1.
	
	
	message := OrderedCollection new.
	message add: 'Entrada por transferencia de 100'.
	message add: 'Balance = 100'.

	
	self assert: message equals: (AccountSummary of: account1) value.! !

!AccountSummaryTest methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:42:52'!
test05AccountSummaryWithSendingTransference

	| account1 account2 message |
	
	account1 := ReceptiveAccount new.
	account2 := ReceptiveAccount new.

	Transference register: 100 from: account2  to: account1.
	
	
	message := OrderedCollection new.
	message add: 'Salida por transferencia de 100'.
	message add: 'Balance = -100'.

	
	self assert: message equals: (AccountSummary of: account2) value.
! !

!AccountSummaryTest methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:43:01'!
test06AccountSummaryWithManyTransactions
	| account1 account2 message |
	
	account1 := ReceptiveAccount new.
	account2 := ReceptiveAccount new.
	Deposit register: 100 on: account1.
	Withdraw register: 50 on: account1.
	Transference register: 20 from: account1  to: account2.
	Transference register: 30 from: account2  to: account1.
	
	message := OrderedCollection new.
	message add: 'Deposito por 100'.
	message add: 'Extraccion por 50'.
	message add: 'Salida por transferencia de 20'.
	message add: 'Entrada por transferencia de 30'.
	message add: 'Balance = 60'.

	
	self assert: message equals:  (AccountSummary of: account1) value.! !

!AccountSummaryTest methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:43:09'!
test07ManyAccountSummaryWithManyTransactions
	| account1 account2 message message2 |
	
	account1 := ReceptiveAccount new.
	account2 := ReceptiveAccount new.
	Deposit register: 100 on: account1.
	Withdraw register: 50 on: account1.
	Transference register: 20 from: account1  to: account2.
	Transference register: 30 from: account2  to: account1.
	
	message := OrderedCollection new.
	message add: 'Deposito por 100'.
	message add: 'Extraccion por 50'.
	message add: 'Salida por transferencia de 20'.
	message add: 'Entrada por transferencia de 30'.
	message add: 'Balance = 60'.
	
	message2 := OrderedCollection new.
	message2 add: 'Entrada por transferencia de 20'.
	message2 add: 'Salida por transferencia de 30'.
	message2 add: 'Balance = -10'.

	
	self assert: message equals:  (AccountSummary of: account1) value.
	self assert: message2 equals:  (AccountSummary of: account2) value.

! !


!classDefinition: #PortfolioTest category: 'Portfolio-Solucion'!
TestCase subclass: #PortfolioTest
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 11:16:26'!
test01BalanceOfPortfolioWithoutAccountsIsZero

	self assert: 0 equals: Portfolio new balance! !

!PortfolioTest methodsFor: 'tests' stamp: 'NR 5/27/2021 17:36:04'!
test02BalanceOfPortfolioWithAccountsIsSumOfAccountsBalance

	| account portfolio |
	
	account := ReceptiveAccount new.
	Deposit register: 100 on: account.
	portfolio := Portfolio with: account.
	
	self assert: account balance equals: portfolio balance! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 11:23:25'!
test03BalanceOfPortfolioIsCalculatedRecursivelyOnPortfolios

	| simplePortfolioAccount simplePortfolio composedPortfolioAccount composedPortofolio |
	
	simplePortfolioAccount := ReceptiveAccount new.
	Deposit register: 100 on: simplePortfolioAccount.
	simplePortfolio := Portfolio with: simplePortfolioAccount.
	
	composedPortfolioAccount := ReceptiveAccount new.
	Withdraw register: 50 on: composedPortfolioAccount.
	composedPortofolio := Portfolio with: simplePortfolio with: composedPortfolioAccount.
	
	self assert: (composedPortfolioAccount balance + simplePortfolio balance) equals: composedPortofolio balance! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 11:43:15'!
test04PortfolioWithoutAccountsHasNoRegisteredTransaction

	self deny: (Portfolio new hasRegistered: (Deposit for: 100))! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 11:43:11'!
test05PortfolioHasRegisteredItsAccountsTransactions

	| account portfolio deposit |
	
	account := ReceptiveAccount new.
	deposit := Deposit register: 100 on: account.
	portfolio := Portfolio with: account.
	
	self assert: (portfolio hasRegistered: deposit)! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 11:58:06'!
test06PortfolioLooksForRegisteredTransactionsRecursively

	| simplePortfolioAccount simplePortfolio composedPortfolioAccount composedPortfolio composedPortfolioAccountWithdraw simplePortfolioAccountDeposit |
	
	simplePortfolioAccount := ReceptiveAccount new.
	simplePortfolioAccountDeposit := Deposit register: 100 on: simplePortfolioAccount.
	simplePortfolio := Portfolio with: simplePortfolioAccount.
	
	composedPortfolioAccount := ReceptiveAccount new.
	composedPortfolioAccountWithdraw := Withdraw register: 50 on: composedPortfolioAccount.
	composedPortfolio := Portfolio with: simplePortfolio with: composedPortfolioAccount.
	
	self assert: (composedPortfolio hasRegistered: simplePortfolioAccountDeposit).
	self assert: (composedPortfolio hasRegistered: composedPortfolioAccountWithdraw)! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 11:58:10'!
test07PortfolioHasNoTransactionWhenHasNoAccounts

	self assert: Portfolio new transactions isEmpty! !

!PortfolioTest methodsFor: 'tests' stamp: 'NR 6/22/2020 07:31:19'!
test08PortfolioTransactionsIncludesAllItsAccountsTransactions

	| account portfolio accountDeposit anotherAccount portfolioTransactions anotherAccountWithdraw |
	
	account := ReceptiveAccount new.
	accountDeposit := Deposit register: 100 on: account.
	anotherAccount := ReceptiveAccount new.
	anotherAccountWithdraw := Withdraw register: 100 on: account.
	portfolio := Portfolio with: account.
	
	portfolioTransactions := portfolio transactions.
	
	self assert: 2 equals: portfolioTransactions size.
	self assert: (portfolioTransactions includes: accountDeposit).
	self assert: (portfolioTransactions includes: anotherAccountWithdraw)! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 11:58:20'!
test09PortfolioTransactionsAreCalculatedRecursively

	| simplePortfolioAccount simplePortfolio composedPortfolioAccount composedPortfolio composedPortfolioAccountWithdraw simplePortfolioAccountDeposit composedPortfolioTransactions |
	
	simplePortfolioAccount := ReceptiveAccount new.
	simplePortfolioAccountDeposit := Deposit register: 100 on: simplePortfolioAccount.
	simplePortfolio := Portfolio with: simplePortfolioAccount.
	
	composedPortfolioAccount := ReceptiveAccount new.
	composedPortfolioAccountWithdraw := Withdraw register: 50 on: composedPortfolioAccount.
	composedPortfolio := Portfolio with: simplePortfolio with: composedPortfolioAccount.
	
	composedPortfolioTransactions := composedPortfolio transactions.
	self assert: 2 equals: composedPortfolioTransactions size.
	self assert: (composedPortfolioTransactions includes: simplePortfolioAccountDeposit).
	self assert: (composedPortfolioTransactions includes: composedPortfolioAccountWithdraw)! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 11:58:24'!
test10PortfolioCanNotIncludeTheSameAccountMoreThanOnce

	| account portfolio |
	
	account := ReceptiveAccount new.
	portfolio := Portfolio with: account.
	
	self 
		should: [ portfolio add: account ]
		raise: Error - MessageNotUnderstood 
		withExceptionDo: [ :anError |
			self assert: Portfolio canNotAddAccountErrorMessage equals: anError messageText.
			self assert: 1 equals: portfolio accountsSize.
			self assert: (portfolio accountsIncludes: account) ]! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 11:58:28'!
test11PortfolioCanNotIncludeAccountOfItsPortfolios

	| account simplePortfolio composedPortfolio |
	
	account := ReceptiveAccount new.
	simplePortfolio := Portfolio with: account.
	composedPortfolio := Portfolio with: simplePortfolio.
	
	self 
		should: [ composedPortfolio add: account ]
		raise: Error - MessageNotUnderstood 
		withExceptionDo: [ :anError |
			self assert: Portfolio canNotAddAccountErrorMessage equals: anError messageText.
			self assert: 1 equals: composedPortfolio accountsSize.
			self assert: (composedPortfolio accountsIncludes: simplePortfolio) ]! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 11:58:32'!
test12PortfolioCanNotIncludeItself

	| account simplePortfolio |
	
	account := ReceptiveAccount new.
	simplePortfolio := Portfolio with: account.
	
	self 
		should: [ simplePortfolio add: simplePortfolio ]
		raise: Error - MessageNotUnderstood 
		withExceptionDo: [ :anError |
			self assert: Portfolio canNotAddAccountErrorMessage equals: anError messageText.
			self assert: 1 equals: simplePortfolio accountsSize.
			self assert: (simplePortfolio accountsIncludes: account) ]! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 12:01:51'!
test13ComposedPortfolioCanNotHaveParentPortfolioAccount

	| account simplePortfolio composedPortfolio |
	
	account := ReceptiveAccount new.
	simplePortfolio := Portfolio new.
	composedPortfolio := Portfolio with: simplePortfolio.
	composedPortfolio add: account.
	
	self 
		should: [ simplePortfolio add: account ]
		raise: Error - MessageNotUnderstood 
		withExceptionDo: [ :anError |
			self assert: Portfolio canNotAddAccountErrorMessage equals: anError messageText.
			self assert: simplePortfolio accountsIsEmpty ]! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 12:12:16'!
test14ComposedPortfolioCanNotHaveAccountOfAnyRootParentRecursively

	| account leftParentPortfolio leftRootParentPortfolio portfolio rightParentPortfolio rightRootParentPortfolio |
	
	account := ReceptiveAccount new.
	portfolio := Portfolio new.
	leftParentPortfolio := Portfolio with: portfolio .
	leftRootParentPortfolio := Portfolio with: leftParentPortfolio.
	leftRootParentPortfolio add: account.
	
	rightParentPortfolio := Portfolio with: portfolio .
	rightRootParentPortfolio := Portfolio with: rightParentPortfolio.
	rightRootParentPortfolio add: account.

	self 
		should: [ portfolio add: account ]
		raise: Error - MessageNotUnderstood 
		withExceptionDo: [ :anError |
			self assert: Portfolio canNotAddAccountErrorMessage equals: anError messageText.
			self assert: portfolio accountsIsEmpty ]! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/29/2019 16:31:18'!
test15PortfolioCanNotIncludeAnyOfTheComposedAccountOfPortfolioToAdd

	| portfolioToAdd portfolioToModify rootPortfolio sharedAccount |
	
	sharedAccount := ReceptiveAccount new.
	portfolioToModify := Portfolio new.
	rootPortfolio := Portfolio with: sharedAccount with: portfolioToModify.
	portfolioToAdd := Portfolio with: sharedAccount.
	
	self 
		should: [ portfolioToModify add: portfolioToAdd ]
		raise: Error - MessageNotUnderstood 
		withExceptionDo: [ :anError | 
			self assert: Portfolio canNotAddAccountErrorMessage equals: anError messageText.
			self assert: portfolioToModify accountsIsEmpty ]! !


!classDefinition: #ReceptiveAccountTest category: 'Portfolio-Solucion'!
TestCase subclass: #ReceptiveAccountTest
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!ReceptiveAccountTest methodsFor: 'tests' stamp: 'HAW 5/23/2019 15:19:48'!
test01ReceptiveAccountHaveZeroAsBalanceWhenCreated 

	| account |
	
	account := ReceptiveAccount new.

	self assert: 0 equals: account balance .
! !

!ReceptiveAccountTest methodsFor: 'tests' stamp: 'HAW 5/23/2019 15:19:54'!
test02DepositIncreasesBalanceOnTransactionValue 

	| account |
	
	account := ReceptiveAccount  new.
	Deposit register: 100 on: account.
		
	self assert: 100 equals: account balance .
! !

!ReceptiveAccountTest methodsFor: 'tests' stamp: 'HAW 5/23/2019 15:20:02'!
test03WithdrawDecreasesBalanceOnTransactionValue 

	| account |
	
	account := ReceptiveAccount new.
	Deposit register: 100 on: account.
	Withdraw register: 50 on: account.
		
	self assert: 50 equals: account balance .
! !

!ReceptiveAccountTest methodsFor: 'tests' stamp: 'HAW 5/23/2019 15:20:32'!
test04WithdrawValueMustBePositive 

	| account withdrawValue |
	
	account := ReceptiveAccount new.
	withdrawValue := 50.
	
	self assert: withdrawValue equals: (Withdraw register: withdrawValue on: account) value
! !

!ReceptiveAccountTest methodsFor: 'tests' stamp: 'HAW 5/23/2019 15:20:46'!
test05ReceptiveAccountKnowsRegisteredTransactions 

	| account deposit withdraw |
	
	account := ReceptiveAccount new.
	deposit := Deposit register: 100 on: account.
	withdraw := Withdraw register: 50 on: account.
		
	self assert: (account hasRegistered: deposit).
	self assert: (account hasRegistered: withdraw).
! !

!ReceptiveAccountTest methodsFor: 'tests' stamp: 'HAW 5/23/2019 15:20:54'!
test06ReceptiveAccountDoNotKnowNotRegisteredTransactions

	| account deposit withdraw |
	
	account := ReceptiveAccount new.
	deposit :=  Deposit for: 100.
	withdraw := Withdraw for: 50.
		
	self deny: (account hasRegistered: deposit).
	self deny: (account hasRegistered:withdraw).
! !

!ReceptiveAccountTest methodsFor: 'tests' stamp: 'HAW 5/23/2019 15:21:24'!
test07AccountKnowsItsTransactions 

	| account1 deposit1 |
	
	account1 := ReceptiveAccount new.
	
	deposit1 := Deposit register: 100 on: account1.
		
	self assert: 1 equals: account1 transactions size .
	self assert: (account1 transactions includes: deposit1).
! !


!classDefinition: #TransferNetTest category: 'Portfolio-Solucion'!
TestCase subclass: #TransferNetTest
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!TransferNetTest methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:43:45'!
test01TransferNetWithBalanceZero

	| account1  |
	
	account1 := ReceptiveAccount new.
			
	self assert: 0 equals: (TransferNet of: account1) value.
! !

!TransferNetTest methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:45:02'!
test02TransferNetWithRecevingTransference

	| account1 account2 |
	
	account1 := ReceptiveAccount new.
	account2 := ReceptiveAccount new.

	Transference register: 100 from: account2  to: account1.
	
	self assert: 100 equals: (TransferNet of: account1) value.! !

!TransferNetTest methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:46:38'!
test03TransferNetWithSendingTransference

	| account1 account2 |
	
	account1 := ReceptiveAccount new.
	account2 := ReceptiveAccount new.

	Transference register: 100 from: account2  to: account1.
	
	self assert: -100 equals: (TransferNet of: account2) value.
! !


!classDefinition: #TransferenceTest category: 'Portfolio-Solucion'!
TestCase subclass: #TransferenceTest
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!TransferenceTest methodsFor: 'as yet unclassified' stamp: 'Ms 5/29/2023 21:35:09'!
test01TransferAccountToAnotherAffectBalance
	| firstAccount secondAccount |
	firstAccount := ReceptiveAccount new.
	secondAccount := ReceptiveAccount new.
	
	Deposit register: 100 on: firstAccount.
	Transference register: 50 from: firstAccount to: secondAccount.
	
	self assert: 50 equals: firstAccount balance.
	self assert: 50 equals: secondAccount balance.! !

!TransferenceTest methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 01:49:46'!
test02AccountsHaveRegisterTransfer
	| firstAccount secondAccount aTransfer  |
	firstAccount := ReceptiveAccount new.
	secondAccount := ReceptiveAccount new.
	
	aTransfer := Transference register: 50 from: firstAccount to: secondAccount.

	self assert: (firstAccount hasRegistered: aTransfer withdrawTransfer).
	self assert: (secondAccount hasRegistered: aTransfer depositTransfer).! !

!TransferenceTest methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 11:51:26'!
test03FirstAccountCanSearchSecondAccount
	| firstAccount secondAccount transfer |
	firstAccount := ReceptiveAccount new.
	secondAccount := ReceptiveAccount new.
	
	transfer := Transference register: 50 from: firstAccount to: secondAccount.
	
	self assert: transfer withdrawTransfer equals: transfer depositTransfer getTransfer.
! !

!TransferenceTest methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 11:51:54'!
test04SecondAccountCanSearchFirstAccount
	| firstAccount secondAccount transfer |
	firstAccount := ReceptiveAccount new.
	secondAccount := ReceptiveAccount new.
	
	transfer := Transference register: 50 from: firstAccount to: secondAccount.
	
	self assert: transfer depositTransfer equals: transfer withdrawTransfer getTransfer.
! !


!classDefinition: #Account category: 'Portfolio-Solucion'!
Object subclass: #Account
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!Account methodsFor: 'testing' stamp: 'HAW 5/25/2019 12:23:47'!
hasRegistered: aTransaction

	self subclassResponsibility ! !

!Account methodsFor: 'testing' stamp: 'HAW 5/25/2019 12:24:25'!
isComposedBy: anAccount

	self subclassResponsibility ! !


!Account methodsFor: 'balance' stamp: 'HAW 5/25/2019 12:23:40'!
balance

	self subclassResponsibility ! !


!Account methodsFor: 'transactions' stamp: 'HAW 5/25/2019 12:23:27'!
addTransactionsTo: aCollectionOfTransactions

	self subclassResponsibility ! !

!Account methodsFor: 'transactions' stamp: 'HAW 5/25/2019 12:23:15'!
transactions

	self subclassResponsibility ! !


!Account methodsFor: 'composition' stamp: 'HAW 5/25/2019 12:24:04'!
addedTo: aPortfolio

	self subclassResponsibility ! !



!classDefinition: #Portfolio category: 'Portfolio-Solucion'!
Account subclass: #Portfolio
	instanceVariableNames: 'accounts parents'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!Portfolio methodsFor: 'accounts management' stamp: 'HAW 5/25/2019 11:49:20'!
accountsIncludes: anAccount

	^accounts includes: anAccount ! !

!Portfolio methodsFor: 'accounts management' stamp: 'HAW 5/25/2019 12:05:04'!
accountsIsEmpty
	
	^accounts isEmpty ! !

!Portfolio methodsFor: 'accounts management' stamp: 'HAW 5/25/2019 11:49:06'!
accountsSize
	
	^accounts size! !

!Portfolio methodsFor: 'accounts management' stamp: 'HAW 5/25/2019 12:19:20'!
add: accountToAdd

	self assertCanAdd: accountToAdd.
		
	accounts add: accountToAdd.
	accountToAdd addedTo: self 
	! !

!Portfolio methodsFor: 'accounts management' stamp: 'HAW 5/25/2019 12:17:31'!
rootParents
	
	| rootParents |
	
	rootParents := Set new.
	self addRootParentsTo: rootParents.
	
	^ rootParents! !


!Portfolio methodsFor: 'initialization' stamp: 'HAW 5/25/2019 12:03:18'!
initialize

	accounts := OrderedCollection new.
	parents := OrderedCollection new.! !


!Portfolio methodsFor: 'balance' stamp: 'HAW 5/25/2019 11:19:36'!
balance
	
	^accounts sum: [ :anAccount | anAccount balance ] ifEmpty: [ 0 ]! !


!Portfolio methodsFor: 'transactions' stamp: 'HAW 5/25/2019 11:42:55'!
addTransactionsTo: aCollectionOfTransactions

	accounts do: [ :anAccount | anAccount addTransactionsTo: aCollectionOfTransactions ]! !

!Portfolio methodsFor: 'transactions' stamp: 'HAW 5/25/2019 11:38:32'!
transactions
	
	| transactions |
	
	transactions := OrderedCollection new.
	accounts do: [ :anAccount | anAccount addTransactionsTo: transactions ].
	
	^transactions ! !


!Portfolio methodsFor: 'composition' stamp: 'HAW 5/25/2019 12:02:59'!
addedTo: aPortfolio 
	
	parents add: aPortfolio ! !


!Portfolio methodsFor: 'testing' stamp: 'HAW 5/25/2019 12:20:56'!
anyRootParentIsComposedBy: accountToAdd

	^self rootParents anySatisfy: [ :aParent | aParent isComposedBy: accountToAdd]! !

!Portfolio methodsFor: 'testing' stamp: 'HAW 5/25/2019 11:28:29'!
hasRegistered: aTransaction

	^accounts anySatisfy: [ :anAccount | anAccount hasRegistered: aTransaction ]! !

!Portfolio methodsFor: 'testing' stamp: 'HAW 5/29/2019 16:24:54'!
isComposedBy: anAccount

	^ self = anAccount or: [ accounts anySatisfy: [ :composedAccount | (composedAccount isComposedBy: anAccount) or: [ anAccount isComposedBy: composedAccount ]]]! !


!Portfolio methodsFor: 'account management - private' stamp: 'HAW 5/25/2019 12:17:31'!
addRootParentsTo: rootParents

	parents 
		ifEmpty: [ rootParents add: self ] 
		ifNotEmpty: [ parents do: [ :aParent | aParent addRootParentsTo: rootParents ]]! !

!Portfolio methodsFor: 'account management - private' stamp: 'HAW 5/25/2019 12:20:36'!
assertCanAdd: accountToAdd

	(self anyRootParentIsComposedBy: accountToAdd) ifTrue: [ self signalCanNotAddAccount ].
! !

!Portfolio methodsFor: 'account management - private' stamp: 'HAW 5/25/2019 11:48:34'!
signalCanNotAddAccount
	
	self error: self class canNotAddAccountErrorMessage! !


!Portfolio methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 02:21:04'!
getMessageTransaction
	^'Deposito por ', self value asString.! !

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "!

!classDefinition: 'Portfolio class' category: 'Portfolio-Solucion'!
Portfolio class
	instanceVariableNames: ''!

!Portfolio class methodsFor: 'as yet unclassified' stamp: 'HAW 5/25/2019 11:48:55'!
canNotAddAccountErrorMessage
	
	^'Can not add repeated account to a portfolio'! !

!Portfolio class methodsFor: 'as yet unclassified' stamp: 'HAW 5/25/2019 11:18:21'!
with: anAccount

	^self new 
		add: anAccount;
		yourself! !

!Portfolio class methodsFor: 'as yet unclassified' stamp: 'HAW 5/25/2019 11:23:59'!
with: anAccount with: anotherAccount

	^self new 
		add: anAccount;
		add: anotherAccount;
		yourself! !


!classDefinition: #ReceptiveAccount category: 'Portfolio-Solucion'!
Account subclass: #ReceptiveAccount
	instanceVariableNames: 'transactions'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!ReceptiveAccount methodsFor: 'initialization' stamp: 'HernanWilkinson 7/13/2011 18:35'!
initialize

	super initialize.
	transactions := OrderedCollection new.! !


!ReceptiveAccount methodsFor: 'transactions' stamp: 'HAW 5/25/2019 11:38:52'!
addTransactionsTo: aCollectionOfTransactions

	aCollectionOfTransactions addAll: transactions ! !

!ReceptiveAccount methodsFor: 'transactions' stamp: 'HernanWilkinson 7/13/2011 18:37'!
register: aTransaction

	transactions add: aTransaction 
! !

!ReceptiveAccount methodsFor: 'transactions' stamp: 'HernanWilkinson 7/13/2011 18:37'!
transactions 

	^ transactions copy! !


!ReceptiveAccount methodsFor: 'balance' stamp: 'Ms 5/31/2023 01:23:44'!
balance

	^transactions 
		inject: 0
		into: [ :currentBalance :transaction | transaction affectBalance: currentBalance ]! !


!ReceptiveAccount methodsFor: 'testing' stamp: 'NR 10/21/2019 18:55:56'!
hasRegistered: aTransaction

	^ transactions includes: aTransaction 
! !

!ReceptiveAccount methodsFor: 'testing' stamp: 'HAW 5/25/2019 11:54:51'!
isComposedBy: anAccount

	^self = anAccount ! !


!ReceptiveAccount methodsFor: 'composition' stamp: 'HAW 5/25/2019 12:03:32'!
addedTo: aPortfolio 
	
	! !



!ReceptiveAccount methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 02:21:04'!
getMessageTransaction
	^'Deposito por ', self value asString.! !

!ReceptiveAccount methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:31:10'!
processReport: aReport

	transactions do: [ :trans |  trans addTransactionToReport: aReport	].
	aReport printBalance: self balance.
! !

!ReceptiveAccount methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 02:39:41'!
showMessageSummary: aSummary

	transactions do: [ :trans |  trans printOnSummaryMessage: aSummary	].
	aSummary printBalance: self balance.
! !


!classDefinition: #AccountTransaction category: 'Portfolio-Solucion'!
Object subclass: #AccountTransaction
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!AccountTransaction methodsFor: 'value' stamp: 'HernanWilkinson 9/12/2011 12:25'!
value 

	self subclassResponsibility ! !


!AccountTransaction methodsFor: 'balance' stamp: 'HAW 5/25/2019 11:25:39'!
affectBalance: aBalance

	self subclassResponsibility ! !

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "!

!classDefinition: 'AccountTransaction class' category: 'Portfolio-Solucion'!
AccountTransaction class
	instanceVariableNames: ''!

!AccountTransaction class methodsFor: 'instance creation' stamp: 'NR 10/21/2019 18:54:27'!
register: aValue on: account

	| transaction |
	
	transaction := self for: aValue.
	account register: transaction.
		
	^ transaction! !


!classDefinition: #Deposit category: 'Portfolio-Solucion'!
AccountTransaction subclass: #Deposit
	instanceVariableNames: 'value'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!Deposit methodsFor: 'initialization' stamp: 'HernanWilkinson 7/13/2011 18:45'!
initializeFor: aValue

	value := aValue ! !


!Deposit methodsFor: 'value' stamp: 'HernanWilkinson 7/13/2011 18:38'!
value

	^ value! !


!Deposit methodsFor: 'balance' stamp: 'Ms 5/31/2023 01:27:01'!
affectBalance: aBalance 

	^aBalance + value ! !


!Deposit methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:31:40'!
addTransactionToReport: aReport
	^ aReport addDepositWithValue: self value.! !

!Deposit methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 02:41:30'!
printOnSummaryMessage: aSummary
	^ aSummary printDepositWithValue: self value.! !

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "!

!classDefinition: 'Deposit class' category: 'Portfolio-Solucion'!
Deposit class
	instanceVariableNames: ''!

!Deposit class methodsFor: 'instance creation' stamp: 'HernanWilkinson 7/13/2011 18:38'!
for: aValue

	^ self new initializeFor: aValue ! !


!classDefinition: #LegDepositTransfer category: 'Portfolio-Solucion'!
AccountTransaction subclass: #LegDepositTransfer
	instanceVariableNames: 'transfer'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!LegDepositTransfer methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:32:12'!
addTransactionToReport: aReport
   ^ aReport addLegDepositWithValue: self value.! !

!LegDepositTransfer methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 01:06:19'!
affectBalance: aBalance
	^aBalance + self value
	! !

!LegDepositTransfer methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 11:51:15'!
getTransfer
	^transfer withdrawTransfer ! !

!LegDepositTransfer methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 01:02:44'!
initializeFrom: aTransfer onAccount: anAccount
	transfer := aTransfer.
	anAccount register: self.! !

!LegDepositTransfer methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 02:42:59'!
printOnSummaryMessage: aSummary
	^ aSummary printLegDepositWithValue: self value.! !

!LegDepositTransfer methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 01:06:14'!
value 
	^transfer value
	! !


"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "!

!classDefinition: 'LegDepositTransfer class' category: 'Portfolio-Solucion'!
LegDepositTransfer class
	instanceVariableNames: ''!

!LegDepositTransfer class methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 01:02:15'!
initializeWithTransfer: aTransfer onAccount: anAccount.
	^self new initializeFrom: aTransfer onAccount: anAccount.
! !


!classDefinition: #LegWithdrawTransfer category: 'Portfolio-Solucion'!
AccountTransaction subclass: #LegWithdrawTransfer
	instanceVariableNames: 'transfer'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!LegWithdrawTransfer methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 01:06:24'!
affectBalance: aBalance
	^aBalance - self value
	! !

!LegWithdrawTransfer methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 11:52:00'!
getTransfer
	^transfer depositTransfer! !

!LegWithdrawTransfer methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 01:03:33'!
initializeFrom: aTransfer onAccount: anAccount
	transfer := aTransfer.
	anAccount register: self.! !

!LegWithdrawTransfer methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 01:05:26'!
value
	^transfer value
	! !


!LegWithdrawTransfer methodsFor: 'category-name' stamp: 'Ms 5/31/2023 12:33:00'!
addTransactionToReport: aReport
   ^ aReport addLegWithdrawWithValue: self value.! !

!LegWithdrawTransfer methodsFor: 'category-name' stamp: 'Ms 5/31/2023 02:42:40'!
printOnSummaryMessage: aSummary
	^ aSummary printLegWithdrawWithValue: self value.! !

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "!

!classDefinition: 'LegWithdrawTransfer class' category: 'Portfolio-Solucion'!
LegWithdrawTransfer class
	instanceVariableNames: ''!

!LegWithdrawTransfer class methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 01:03:20'!
initializeWithTransfer: aTransfer onAccount: anAccount.
	^self new initializeFrom: aTransfer onAccount: anAccount.
! !


!classDefinition: #Withdraw category: 'Portfolio-Solucion'!
AccountTransaction subclass: #Withdraw
	instanceVariableNames: 'value'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!Withdraw methodsFor: 'initialization' stamp: 'HernanWilkinson 7/13/2011 18:46'!
initializeFor: aValue

	value := aValue ! !


!Withdraw methodsFor: 'value' stamp: 'HernanWilkinson 7/13/2011 18:33'!
value

	^ value! !


!Withdraw methodsFor: 'balance' stamp: 'Ms 5/31/2023 01:26:50'!
affectBalance: aBalance

	^aBalance - value! !


!Withdraw methodsFor: 'category-name' stamp: 'Ms 5/31/2023 12:32:29'!
addTransactionToReport: aReport
   ^ aReport addWithdrawWithValue: self value.! !

!Withdraw methodsFor: 'category-name' stamp: 'Ms 5/31/2023 02:41:55'!
printOnSummaryMessage: aSummary
	^ aSummary printWithdrawWithValue: self value.! !

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "!

!classDefinition: 'Withdraw class' category: 'Portfolio-Solucion'!
Withdraw class
	instanceVariableNames: ''!

!Withdraw class methodsFor: 'instance creation' stamp: 'HernanWilkinson 7/13/2011 18:33'!
for: aValue

	^ self new initializeFor: aValue ! !


!classDefinition: #Reporte category: 'Portfolio-Solucion'!
Object subclass: #Reporte
	instanceVariableNames: 'account'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!Reporte methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:35:16'!
addDepositWithValue: aValue
	self subclassResponsibility ! !

!Reporte methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:35:11'!
addLegDepositWithValue: aValue
	self subclassResponsibility ! !

!Reporte methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:35:30'!
addLegWithdrawWithValue: aValue
	self subclassResponsibility ! !

!Reporte methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:35:21'!
addWithdrawWithValue: aValue
	self subclassResponsibility ! !

!Reporte methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:40:46'!
intializeWithAccount: anAccount
	account  := anAccount.
	account processReport: self.

! !

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "!

!classDefinition: 'Reporte class' category: 'Portfolio-Solucion'!
Reporte class
	instanceVariableNames: ''!

!Reporte class methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:26:11'!
of: anAccount
	^self new intializeWithAccount: anAccount.! !


!classDefinition: #AccountSummary category: 'Portfolio-Solucion'!
Reporte subclass: #AccountSummary
	instanceVariableNames: 'message'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!AccountSummary methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:33:50'!
addDepositWithValue: aValue
	^message add: 'Deposito por ', aValue asString.! !

!AccountSummary methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:34:09'!
addLegDepositWithValue: aValue
	^message add:  'Entrada por transferencia de ', aValue asString.! !

!AccountSummary methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:34:19'!
addLegWithdrawWithValue: aValue
	^message add:  'Salida por transferencia de ', aValue asString.! !

!AccountSummary methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:34:35'!
addWithdrawWithValue: aValue
	^message add: 'Extraccion por ', aValue asString.! !

!AccountSummary methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:39:40'!
initialize
	message := OrderedCollection new.! !

!AccountSummary methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 02:51:42'!
printBalance: aValue
	^message add: 'Balance = ', aValue asString.! !

!AccountSummary methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:41:29'!
value
	^message! !


!classDefinition: #TransferNet category: 'Portfolio-Solucion'!
Reporte subclass: #TransferNet
	instanceVariableNames: 'balance'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!TransferNet methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:35:58'!
addDepositWithValue: aValue
	 ! !

!TransferNet methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:36:36'!
addLegDepositWithValue: aValue
	balance := balance + aValue	 ! !

!TransferNet methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:36:47'!
addLegWithdrawWithValue: aValue
	balance := balance - aValue	 ! !

!TransferNet methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:35:54'!
addWithdrawWithValue: aValue
	 ! !

!TransferNet methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:39:24'!
initialize
	balance := 0! !

!TransferNet methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:37:56'!
printBalance: aValue
	^'Balance = ', aValue asString.! !

!TransferNet methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 12:41:38'!
value
	^balance! !


!classDefinition: #Transference category: 'Portfolio-Solucion'!
Object subclass: #Transference
	instanceVariableNames: 'value withdraw deposit'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!Transference methodsFor: 'as yet unclassified' stamp: 'Ms 5/31/2023 02:18:32'!
intializeValue: aValue from: firstAccount to: secondAccount
	value := aValue.
	withdraw := LegWithdrawTransfer initializeWithTransfer: self onAccount: firstAccount.
	deposit := LegDepositTransfer initializeWithTransfer: self onAccount: secondAccount.
! !

!Transference methodsFor: 'as yet unclassified' stamp: 'Ms 5/30/2023 23:25:38'!
value
	^value ! !


!Transference methodsFor: 'category-name' stamp: 'Ms 5/31/2023 01:44:41'!
depositTransfer  
	^deposit ! !

!Transference methodsFor: 'category-name' stamp: 'Ms 5/31/2023 01:44:35'!
withdrawTransfer  
	^withdraw! !

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "!

!classDefinition: 'Transference class' category: 'Portfolio-Solucion'!
Transference class
	instanceVariableNames: ''!

!Transference class methodsFor: 'category-name' stamp: 'Ms 5/30/2023 16:36:35'!
accountNotIncludedInTransfer
	^'Account Not Incuded In This Transfer'! !

!Transference class methodsFor: 'category-name' stamp: 'Ms 5/29/2023 21:41:39'!
register: aValue from: firstAccount to: secondAccount 
	^self new intializeValue: aValue from: firstAccount to: secondAccount.! !
